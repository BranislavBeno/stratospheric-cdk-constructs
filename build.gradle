plugins {
    id 'java'
    id 'com.jfrog.bintray' version '1.8.1'
    id 'maven-publish'
}

ext{
    bintrayUser = System.getProperty("bintray.user")
    bintrayKey = System.getProperty("bintray.key")
    buildNumber = System.getProperty("build.number")
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'dev.stratospheric'
version = '0.0.11'
sourceCompatibility = '11'

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

ext {
    cdkVersion = '1.79.0'
}

dependencies {
    implementation "software.amazon.awscdk:core:$cdkVersion"
    implementation "software.amazon.awscdk:ec2:$cdkVersion"
    implementation "software.amazon.awscdk:elasticloadbalancingv2:$cdkVersion"
    implementation "software.amazon.awscdk:autoscaling:$cdkVersion"
    implementation "software.amazon.awscdk:rds:$cdkVersion"
    implementation "software.amazon.awscdk:secretsmanager:$cdkVersion"
    implementation "software.amazon.awscdk:ecr:$cdkVersion"
    implementation "software.amazon.awscdk:iam:$cdkVersion"
    implementation "software.amazon.awscdk:ecs:$cdkVersion"
    implementation "software.amazon.awscdk:ssm:$cdkVersion"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "MIT"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "thombergs"
            name "Tom Hombergs"
            email "tom.hombergs@gmail.com"
        }
    }

    scm {
        url "https://github.com/stratospheric-dev/cdk-constructs"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'dev.stratospheric'
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A collection of CDK constructs to deploy an application on AWS.')
                root.appendNode('name', 'cdk-constructs')
                root.appendNode('url', 'https://github.com/stratospheric-dev/cdk-constructs')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['mavenPublication']

    pkg {
        repo = 'maven-releases'
        name = 'cdk-constructs'
        userOrg = 'stratospheric'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/stratospheric-dev/cdk-constructs'
        version {
            name = project.version
            desc = "build ${buildNumber}"
            released  = new Date()
        }
    }

    publish = true
}


